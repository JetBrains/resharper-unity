def backendGroup = 'backend'

import com.jetbrains.rider.plugins.gradle.BackendPaths
import com.jetbrains.rider.plugins.gradle.tasks.*

ext.backend = new BackendPaths(project, logger, ext.repoRoot, ext.productVersion)

nuget {
    version = "4.9.4"
}

task prepareRiderBuildProps(type: GenerateDotNetSdkPathPropsTask) {
    dotNetSdkPath { backend.getDotNetSdkPath() }
}

task prepareRdFrameworkNet35Props {
    dependsOn prepareRiderBuildProps
    doLast{
        def dotNetSdkPath = backend.getDotNetSdkPath()
        logger.warn("dotNetSdkPath='$dotNetSdkPath'")
        assert dotNetSdkPath.isDirectory()
        def propsFolder = new File(project.projectDir.parent, "unity/build/generated")
        def propsFile = new File(propsFolder, "PackageReference.JetBrains.RdFramework.net35.Props")
        buildServer.progress("Generating $propsFile")
        propsFolder.mkdirs()
        propsFile.createNewFile()
        propsFile.text = new File("$dotNetSdkPath/Build/PackageReference.JetBrains.RdFramework.Props").text.replace("JetBrains.RdFramework", "JetBrains.RdFramework.net35")
    }
}

task prepareNuGetConfig {
    dependsOn prepareRiderBuildProps

    doLast {
        def dotNetSdkPath = backend.getDotNetSdkPath()
        logger.warn("dotNetSdkPath='$dotNetSdkPath'")
        assert dotNetSdkPath.isDirectory()

        buildServer.progress("Preparing NuGet.Config")

        def nugetConfigText = """<?xml version="1.0" encoding="utf-8"?>
    <configuration>
    <packageSources>
        <add key="local-dotnet-sdk" value="$dotNetSdkPath" />
    </packageSources>
    </configuration>
    """
        logger.info(nugetConfigText)
        new File("$repoRoot/resharper/NuGet.Config").text = nugetConfigText
    }
}

task restoreReSharperHostPluginPackages(type: nugetRestore.class) {
    group backendGroup
    description 'Restores packages for backend plugin'
    dependsOn prepareNuGetConfig, prepareRdFrameworkNet35Props

    solutionFile backend.resharperHostPluginSolution
    noCache = true
    sources = [
            'https://api.nuget.org/v3/index.json'
    ]
    doFirst {
        project.buildServer.progress("Restoring packages for $solutionFile")

        // Add in doFirst, so we get a chance to download the SDKs
        sources.add(project.backend.getDotNetSdkPath())
    }
}

task restoreUnityEditorPluginPackages(type: nugetRestore.class) {
    group backendGroup
    description 'Restores packages for Unity Editor plugin'

    solutionFile backend.unityPluginSolution
    noCache = true
    sources = [
            'https://api.nuget.org/v3/index.json'
    ]
    doFirst {
        project.buildServer.progress("Restoring packages for $solutionFile")

        // Add in doFirst, so we get a chance to download the SDKs
        sources.add(project.backend.getDotNetSdkPath())
    }
}


task buildReSharperHostPlugin(type: MSBuildTask) {
    group restoreReSharperHostPluginPackages
    description 'Builds the full ReSharper backend plugin solution'
    dependsOn restoreReSharperHostPluginPackages, generateModel

    buildFile = backend.resharperHostPluginSolution
}

task buildUnityEditorPlugin(type: MSBuildTask) {
    group backendGroup
    description 'Builds the Unity editor plugin'
    dependsOn restoreUnityEditorPluginPackages, generateModel

    buildFile = backend.unityPluginSolution
}


task packReSharperPlugin(type: nugetPack.class) {
    dependsOn buildReSharperHostPlugin
    group = backendGroup
    description = 'Packs resulting DLLs into a NuGet package which is an R# extension.'

    def changelogNotes = changelog.get(productVersion).withFilter({ line ->
        !line.startsWith("- Rider:") && !line.startsWith("- Unity editor:")
    }).toPlainText().trim()
    def ReleaseNotes = """New in $productVersion

${changelogNotes}

See CHANGELOG.md in the JetBrains/resharper-unity GitHub repo for more details and history."""

    nuspecFile = new File(backend.backendRoot, 'resharper-unity/src/resharper-unity.resharper.nuspec')
    destinationDir = new File(backend.backendRoot, "build/distributions/$BuildConfiguration")
    packageAnalysis = false
    packageVersion = version
    properties = [
            'Configuration': BuildConfiguration,
            'ReleaseNotes': ReleaseNotes
    ]

    def nuPkgFile = file(new File(destinationDir as String, "JetBrains.Unity.${version}.nupkg"))
    outputs.files(nuPkgFile)

    doFirst {
        buildServer.progress("Packing: $nuspecFile.name")
    }
}

task nunitReSharperYaml(type: nunit.class) {
    nunitVersion "3.9.0"
    shadowCopy false
    outputs.upToDateWhen { false }

    def buildDir = new File(backend.repositoryRoot, "resharper/build")
    def testDll = new File(buildDir, "tests.resharper-yaml/bin/$BuildConfiguration/net461/JetBrains.ReSharper.Plugins.Yaml.Tests.ReSharper.dll")
    testAssemblies = [ testDll ]
}

task nunitRiderYaml(type: nunit.class) {
    nunitVersion "3.9.0"
    shadowCopy false
    outputs.upToDateWhen { false }

    def buildDir = new File(backend.repositoryRoot, "resharper/build")
    def testDll = new File(buildDir, "tests.rider-yaml/bin/$BuildConfiguration/net461/JetBrains.ReSharper.Plugins.Yaml.Tests.Rider.dll")
    testAssemblies = [ testDll ]
}

task nunitReSharperUnity(type: nunit.class) {
    nunitVersion "3.9.0"
    shadowCopy false
    outputs.upToDateWhen { false }

    def buildDir = new File(backend.repositoryRoot, "resharper/build")
    def testDll = new File(buildDir, "tests.resharper-unity/bin/$BuildConfiguration/net461/JetBrains.ReSharper.Plugins.Unity.Tests.ReSharper.dll")
    testAssemblies = [ testDll ]
}

task nunitRiderUnity(type: nunit.class) {
    nunitVersion "3.9.0"
    shadowCopy false
    outputs.upToDateWhen { false }

    def buildDir = new File(backend.repositoryRoot, "resharper/build")
    def testDll = new File(buildDir, "tests.rider-unity/bin/$BuildConfiguration/net461/JetBrains.ReSharper.Plugins.Unity.Tests.Rider.dll")
    testAssemblies = [ testDll ]
}

task runNunit {
    // nunit3 defaults to running test assemblies in parallel, which causes problems with shared access to databases
    // The nunit plugin doesn't have the ability to disable this, so we'll do it long hand...
    dependsOn buildReSharperHostPlugin, nunitReSharperYaml, nunitRiderYaml, nunitReSharperUnity, nunitRiderUnity
}

task publishCiBackendArtifacts {
    inputs.files(packReSharperPlugin.outputs)

    doLast {
        buildServer.publishArtifact(packReSharperPlugin.outputs.files.singleFile)
    }
}
