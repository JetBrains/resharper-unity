sourceSets {
    test {
        java.srcDir 'testSrc'
        resources.srcDir 'testData'
    }
}

def dependenciesDir = new File("dependencies")

intellij {
    pluginName 'rider-unity'
    type 'RD'

    // Download a version of Rider to compile and run with. Either set `version` to
    // 'LATEST-TRUNK-SNAPSHOT' or 'LATEST-EAP-SNAPSHOT' or a known version.
    // This will download from www.jetbrains.com/intellij-repository/snapshots or
    // www.jetbrains.com/intellij-repository/releases, respectively.
    // http://jetbrains-com-mirror.labs.intellij.net/intellij-repository/snapshots/
    // Note that there's no guarantee that these are kept up to date
    // version = 'LATEST-TRUNK-SNAPSHOT'
    // If the build isn't available in intellij-repository, use an installed version via `localPath`
    // localPath = '/Users/matt/Library/Application Support/JetBrains/Toolbox/apps/Rider/ch-1/171.4089.265/Rider EAP.app/Contents'
    // localPath = "F:\\RiderSDK"

    if (dependenciesDir .exists()) {
        localPath = dependenciesDir.absolutePath
    } else {
        version = "2018.2-SNAPSHOT"
    }

    // Sources aren't available for Rider
    downloadSources = false
}

def frontendSdkRoot = dependenciesDir
// bundled build configuration on TC
if (dependenciesDir.exists() && dependenciesDir.isDirectory()) {
    logger.lifecycle("dependencies dir found, using as frontend SDK root")    
}
else {
    def frontendSdkVersion = "2018.2-SNAPSHOT"
    // rider/build/riderRD-2018.2-SNAPSHOT
    def frontendSdkDirs = []
    def frontendSdkSearchPath = new File(repoRoot, "rider/build")
    //logger.lifecycle("listing $frontendSdkSearchPath.path:")
    //fileTree(frontendSdkSearchPath).visit {
        //if (it.isDirectory()){
            //if (it.name.startsWith("riderRD"))
                //frontendSdkDirs << it
            //logger.lifecycle("DIR:  $it.name")
        //}
    //}

    if (frontendSdkDirs.size() > 0) {
        assert frontendSdkDirs.size() == 1

        def frontendSdkSingleDir = frontendSdkDirs.first()
        def match = frontendSdkSingleDir.name =~ /riderRD-(.+)/
        assert match.matches()

        frontendSdkVersion = match[0][1]
    }

    frontendSdkRoot = new File(frontendSdkSearchPath, "riderRD-$frontendSdkVersion")
}

ext.rdLibDirectory = new File(frontendSdkRoot, "lib/rd").canonicalFile

ext.kotlin_version = '1.2.10'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs rdLibDirectory.absolutePath
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
}

// TODO: Add an msbuild task for this, too?
// Because we sometimes skip building the C# solutions (calling runIde from the C# IDE), we might
// get into the situation where we rebuild the protocol for .kt, but not .cs
// Maybe we should have an msbuild task to run rdgen if the input model .kt is newer than the output
// .cs. Or make sure we don't skip the msbuild tasks if this task runs
task generateModel {
    group = 'other'
    description = 'Generates protocol models.'

    def rdgenJar = new File(rdLibDirectory.absolutePath, "rd-gen.jar")

    doLast {
        assert rdLibDirectory.isDirectory()
        assert rdgenJar.isFile()

        // If we specify these as outputs of the task, gradle will create them for us, but we then
        // need to also specify inputs. RdGen does up to date checks for us, but we could skip that
        // altogether if we hard code them in the task. Downside is that we'd need to keep the list
        // of inputs/outputs up to date manually, instead of leaving it to rdgen
        mkdir 'build/rdgen/rider'
        mkdir 'build/rdgen/editorPlugin'

        // TODO: spaces (what does this mean?)
        def modelDir = new File(repoRoot, "rider/protocol/src/main/kotlin/model")
        def packageName = "model"

        def cpSeparator = isWindows ? ";" : ":"
        def compilerClasspath = "$rdLibDirectory/rd-gen.jar$cpSeparator$rdLibDirectory/rider-model.jar"
        def rdGenClasspath = files(compilerClasspath)

        // Protocol between backend and frontend
        // Direction isn't important for this model, so we don't specify it in the model. Which means
        // we have to specify output directories here
        // Inputs: rider/protocol/src/main/kotlin/model/rider/RdUnityModel.kt
        // Outputs: resharper/src/resharper-unity/Rider/RdUnityProtocol/RdUnityModel.Generated.cs
        //          rider/src/main/kotlin/com/jetbrains/rider/protocol/RdUnityProtocol/RdUnityModel.Generated.kt
        def csOutput = new File(repoRoot, "resharper/src/resharper-unity/Rider/RdUnityProtocol")
        def ktOutput = new File(repoRoot, "rider/src/main/kotlin/com/jetbrains/rider/protocol/RdUnityProtocol")
        javaexec {
            main = "com.jetbrains.rider.generator.nova.MainKt"
            classpath = rdGenClasspath
            systemProperty "model.out.src.cs.dir", "$csOutput"
            systemProperty "model.out.src.kt.dir", "$ktOutput"
            errorOutput = System.out // kotlin throws warnings in stderr which causes appveyor build to fail
            args = [
                "--verbose",
                "--hash-folder=build/rdgen/rider",
                "--compiler-classpath=$compilerClasspath",
                "--source=${modelDir}/rider",
                "--packages=${packageName}.rider"
            ]
        }

        // TODO: if placed in one dir with different packages there will be generator clash

        // Protocol between Unity plugin (host), frontend and backend
        // Direction is specified in the model, along with output folders
        // Inputs: rider/protocol/src/main/kotlin/model/editorPlugin/EditorPluginModel.kt
        // Outputs: resharper/src/resharper-unity/Rider/RdEditorProtocol/EditorPluginModel.Generated.cs (backend to Unity)
        //          unity/JetBrains.Rider.Unity.Editor/EditorPlugin/NonUnity/RdEditorProtocol/EditorPluginModel.Generated.cs (Unity listener)
        //          rider/src/main/kotlin/com/jetbrains/rider/protocol/RdEditorProtocol/EditorPluginModel.Generated.kt (frontend to Unity)
        javaexec {
            main = "com.jetbrains.rider.generator.nova.MainKt"
            classpath = rdGenClasspath
            errorOutput = System.out // kotlin throws warnings in stderr which causes appveyor build to fail
            args = [
                "--verbose",
                "--hash-folder=build/rdgen/editorPlugin",
                "--compiler-classpath=$compilerClasspath",
                "--source=${modelDir}/editorPlugin",
                "--packages=${packageName}.editorPlugin"
            ]
        }
    }
}


// When running in CI, we call `gradle buildPlugin`
// When running interactively, we call `gradle runIde`
// When running from the open rider solution, we call `gradle runIde -PDotNetUpToDate=true`
//   to skip slow, no-op nuget restore and msbuild. Maybe .NET Core 2.1 will speed that up for us

// buildPlugin depends on prepareSandbox, then zips up the sandbox dir
// runIde depends on prepareSandbox, then executes IJ inside that dir
// prepareSandbox dependsOn standard java build/jar then copies everything to the sandbox dir

// When calling buildPlugin, we should build everything on the backend
// When calling runIde, we should only build Rider and the Unity editor
// prepareSandbox should also make sure that all of the Rider and Unity editor bits are copied
//   (who owns this, frontend, or backend?)


task publishCiBuildNumber {
    doLast {
        ci.SetBuildNumber(version)
    }
}

task publishCiBuildData {
    if (project.isBuildingUnderCi) {
        dependsOn publishCiBuildNumber
        dependsOn publishCiBackendArtifacts
    }
}

buildPlugin.finalizedBy publishCiBuildData
buildPlugin.dependsOn generateModel


// If we're building `runIde`, only build Rider bits. We only know this once the task graph
// has been evaluated. It's now too late to change inputs/outputs or dependencies. An alternative
// approach would be to set the appropriate tasks `enabled` flag
gradle.taskGraph.whenReady { taskGraph ->
    project.shouldBuildRiderOnly = taskGraph.hasTask(runIde) && !taskGraph.hasTask(buildPlugin)
}


def dllFiles = [
    "../resharper/build/resharper-unity.rider/bin/$BuildConfiguration/net452/JetBrains.ReSharper.Plugins.Unity.dll",
    "../resharper/build/resharper-unity.rider/bin/$BuildConfiguration/net452/JetBrains.ReSharper.Plugins.Unity.pdb",
]

def annotationsFrom = '../resharper/src/resharper-unity/annotations'
def annotationsTo = 'Extensions/JetBrains.Unity/annotations'

def unityeditorplugin_path = "$projectDir/../unity/Assets/Plugins/Editor/JetBrains/JetBrains.Rider.Unity.Editor.Plugin.Repacked.dll"
def unityeditorplugin_full_path = "$projectDir/../unity/Assets/Plugins/Editor/JetBrains/JetBrains.Rider.Unity.Editor.Plugin.Full.Repacked.dll"

prepareSandbox {
    // Already depends on the standard Java build/jar task
    dependsOn buildReSharperHostPlugin, buildUnityEditorPlugin

    // Have dependent tasks use uptodateWhen { project.isRunningUnderCi etc. }
    //inputs.files(buildRiderPlugin.outputs)
    //inputs.files(buildUnityEditorPlugin.packedPath)

    // Backend:
    // Copy unity editor plugin repacked file to `rider-unity/EditorPlugin`
    // Copy JetBrains.ReSharper.Plugins.Unity.dll to `rider-unity/dotnet`
    // Copy annotations to `rider-unity/dotnet/Extensions/JetBrains.Unity/annotations`

    // Frontend:
    // Copy projectTemplates to `rider-unity/projectTemplates`

    // Assert these files exist?

    doLast {
        assert file(unityeditorplugin_path).exists()
        assert file(unityeditorplugin_full_path).exists()

        dllFiles.forEach({ f ->
            def file = file("$f")
            if (!file.exists()) throw new RuntimeException("File $file does not exist")
        })
    }

    from(unityeditorplugin_path, {
        into "$intellij.pluginName/EditorPlugin"
    })

    from(unityeditorplugin_full_path, {
        into "$intellij.pluginName/EditorPlugin"
    })

    dllFiles.forEach({ f ->
        def file = file(f)
        from(file, { into "$intellij.pluginName/dotnet" })
    })

    from(annotationsFrom, { into "$intellij.pluginName/dotnet/$annotationsTo" })
    into("$intellij.pluginName/projectTemplates") {
        from 'projectTemplates'
    }
}

test {
    useTestNG() {}
    testLogging {
        showStandardStreams = true
        exceptionFormat "full"
    }
}
